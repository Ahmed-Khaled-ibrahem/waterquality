#include <Wire.h>
#include <LiquidCrystal_I2C.h>

LiquidCrystal_I2C lcd(0x27, 16, 2);

const int fsrPin = A0;
const int buzzerPin = 9;
const int ledPin = 8;
const int resetButton = 7;
const int btn2Rak3at = 4;
const int btn3Rak3at = 5;
const int btn4Rak3at = 6;

int sujoodCount = 0;
int rakahCount = 0;
int targetRakah = 2;
bool inSujood = false;

void setup() {
  pinMode(fsrPin, INPUT);
  pinMode(buzzerPin, OUTPUT);
  pinMode(ledPin, OUTPUT);
  pinMode(resetButton, INPUT);
  pinMode(btn2Rak3at, INPUT);
  pinMode(btn3Rak3at, INPUT);
  pinMode(btn4Rak3at, INPUT);

  lcd.init();
  lcd.backlight();
  lcd.setCursor(0, 0);
  lcd.print("Rak'ah Counter");
  delay(2000);
  lcd.clear();
  displayStatus();
  Serial.begin(9600);
}

void loop() {

  if (digitalRead(btn2Rak3at) == HIGH) {
    Serial.println("btn 1 clicked");
    while(digitalRead(btn2Rak3at) == HIGH){ delay(10);}
    targetRakah = 2;
    resetCounter();
    delay(300);
  }
  if (digitalRead(btn3Rak3at) == HIGH) {
    Serial.println("btn 2 clicked");
     while(digitalRead(btn3Rak3at) == HIGH){ delay(10);}
    targetRakah = 3;
    resetCounter();
    delay(300);
  }
  if (digitalRead(btn4Rak3at) == HIGH) {
    Serial.println("btn 3 clicked");
     while(digitalRead(btn4Rak3at) == HIGH){ delay(10);}
    targetRakah = 4;
    resetCounter();
    delay(300);
  }

  int fsrValue = analogRead(fsrPin);

  if (fsrValue > 100 && !inSujood) {
    inSujood = true;
    sujoodCount++;

    if (sujoodCount % 2 == 0) {
      rakahCount++;
      displayStatus();
      checkIfDone();
    }
    delay(500);  // Debounce delay
  }

  if (fsrValue < 100 && inSujood) {
    inSujood = false; // Reset for next sujood detection
  }

  if (digitalRead(resetButton) == HIGH) {
    while(digitalRead(resetButton) == HIGH){ delay(10);}
    resetCounter();
    delay(300);
  }
}

void displayStatus() {
  lcd.clear();
  lcd.setCursor(0, 0);
  lcd.print("Rak'ah: ");
  lcd.print(rakahCount);
  lcd.setCursor(0, 1);
  lcd.print("Target: ");
  lcd.print(targetRakah);
}

void checkIfDone() {
  if (rakahCount >= targetRakah) {
    digitalWrite(buzzerPin, HIGH);
    digitalWrite(ledPin, HIGH);
    lcd.clear();
    lcd.setCursor(0, 0);
    lcd.print("Prayer Done :)");
    delay(3000);
    digitalWrite(buzzerPin, LOW);
    digitalWrite(ledPin, LOW);
    resetCounter();
  }
}

void resetCounter() {
  sujoodCount = 0;
  rakahCount = 0;
  displayStatus();
}
